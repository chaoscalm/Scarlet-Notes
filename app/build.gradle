plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
}

android {
    compileSdk rootProject.ext.compile_sdk_version
    namespace 'com.maubis.scarlet.base'

    defaultConfig {
        applicationId "fs00.scarletnotes"
        minSdk rootProject.ext.min_sdk_version
        targetSdk rootProject.ext.target_sdk_version
        versionCode 1
        versionName '8.0.0'
        resConfigs 'en', 'it'

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
        kapt {
            arguments {
                arg("room.schemaLocation", "$projectDir/schemas")
            }
        }
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildFeatures {
        viewBinding true
    }

    lint {
        disable 'ContentDescription', 'Autofill', 'LabelFor', 'IconLocation'
    }

    signingConfigs {
        release {
            def signingConfigFile = new File('signing.properties')
            if (signingConfigFile.exists()) {
                def props = new Properties()
                props.load(new FileInputStream(signingConfigFile))
                storeFile file(props['storeFile'])
                storePassword props['storePassword']
                keyAlias props['keyAlias']
                keyPassword props['keyPassword']
            }
        }
    }

    buildTypes {
        debug {
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    sourceSets {
        androidTest.assets.srcDirs += files("$projectDir/schemas".toString())
    }

    kotlinOptions {
        jvmTarget = '1.8'
        freeCompilerArgs += '-opt-in=kotlinx.coroutines.DelicateCoroutinesApi,kotlinx.coroutines.ExperimentalCoroutinesApi'
    }
}

dependencies {
    implementation project(':markdown')

    implementation 'androidx.recyclerview:recyclerview:1.2.1'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.appcompat:appcompat:1.6.0'
    implementation "androidx.activity:activity-ktx:1.6.1"
    implementation "androidx.core:core-ktx:1.9.0"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:2.5.1"

    implementation 'com.google.android.material:material:1.2.1'

    implementation 'com.google.code.gson:gson:2.9.0'

    implementation "com.github.bijoysingh:android-basics:5.1.0-x"
    implementation "com.github.bijoysingh:ui-basics:1.0.0-x"

    implementation 'com.evernote:android-job:1.4.3'

    implementation 'com.google.android.flexbox:flexbox:3.0.0'

    implementation 'androidx.room:room-runtime:2.5.0'
    kapt 'androidx.room:room-compiler:2.5.0'

    implementation "androidx.biometric:biometric:1.1.0"

    implementation 'com.github.jkwiecien:EasyImage:2.1.1'

    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.6.4'
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.6.4"

    def litho_version = "0.46.0"
    implementation "com.facebook.litho:litho-core:$litho_version"
    implementation "com.facebook.litho:litho-widget:$litho_version"
    kapt "com.facebook.litho:litho-processor:$litho_version"
    implementation 'com.facebook.soloader:soloader:0.10.5'
}
